*DECK SFDAT
      PROGRAM SFDAT
C
C     PACKAGE HQ
C     HEAVY-QUARK PRODUCTION IN LO APPROXIMATION
C     APPLICATION PROGRAM
C
C     COMPUTE STRUCTURE FUNCTION FOR SET OF (X, Q2) POINTS
C
      IMPLICIT DOUBLE PRECISION (A - H, O - Z)
C
C     ...VARIABLE FOR PDF SET
C
      COMMON /PDFI/ IPDF
C
C     ...ARRAY FOR INPUT DATA
C
      CHARACTER*80 LINE
      PARAMETER (NDATM = 201, NCOLM = 3, NCOL = 3)
      DIMENSION FDAT(NDATM, NCOLM)
C
      OPEN(1, FILE = 'SFDAT.IN',  STATUS = 'OLD')
      OPEN(2, FILE = 'SFDAT.OUT', STATUS = 'OLD')
      REWIND(1)
      REWIND(2)
C
C     ...READ DATA FROM FILE
C
      NDAT = 0
10000 READ(1, '(A80)', END = 10001) LINE
      IF (LINE(1:1).EQ.'C'.OR.LINE(1:1).EQ.'#') GOTO 10000
      READ(LINE, *) (FDAT(NDAT + 1, I), I = 1, NCOL)
      NDAT = NDAT + 1
      IF (NDAT.GT.NDATM) STOP 'NDAT TOO LARGE'
      GOTO 10000
10001 CONTINUE
      CLOSE(1)
C      
C     ...WRITE OUTPUT FILE HEADER
C
      WRITE(2, 91000) 'OUTPUT SFDAT'
      WRITE(2, 91000) 'NUCLEON HEAVY-QUARK SF IN LO APPROX'
      WRITE(2, 91000) 'COMPUTE F2H FOR SET OF (X, Q2) POINTS'
      WRITE(2, 91000)
      WRITE(2, 91000) 'COL 1  X  (INPUT)'
      WRITE(2, 91000) 'COL 2  QQ (INPUT)'
      WRITE(2, 91000) 'COL 3  RELERR = DELTA-F2H/F2H (INPUT)'
      WRITE(2, 91000) 'COL 4  CENTRAL F2H (OUTPUT)'
      WRITE(2, 91000) 'COL 5  HESSIAN ERROR F2HERR (OUTPUT)'
91000 FORMAT('#*',                A)
91001 FORMAT('#', T3, F12.6, T20, A)
91002 FORMAT('#', T3, I12,   T20, A)
91003 FORMAT('#', T3, E14.7, T20, A)
C
      DO 10002 I = 1, NDAT
C
         X      = FDAT(I, 1)
         QQ     = FDAT(I, 2)
         RELERR = FDAT(I, 3)
C
C     ...CALCULATE F2H FOR CENTRAL SET
C
         IPDF = 1
         CALL SFH(F2H, X, QQ, 1)
C
C     ...CALCULATE HESSIAN ERROR
C
         DELSQ = 0.D0
C
         DO 10003 J = 1, 15
C
            IPDF = 2*J
            CALL SFH(F2HP, X, QQ, 1)
C
            IPDF = 2*J + 1
            CALL SFH(F2HM, X, QQ, 1)
C
            DELSQ = DELSQ + (F2HP - F2HM)**2/4
C
10003    CONTINUE
C
         F2HERR = SQRT(DELSQ)
C
         WRITE(2, 90001) X, QQ, RELERR, F2H, F2HERR
90001    FORMAT(5(1X, E13.6))
C
10002 CONTINUE
C
      END
C
C---------------------------------------------------------------------
C
